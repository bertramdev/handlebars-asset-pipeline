apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
version = '2.1.3'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}


repositories {
	mavenLocal()
    jcenter()
    mavenCentral()

}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
	compile "com.bertramlabs.plugins:asset-pipeline-core:2.1.0"
	provided 'org.codehaus.groovy:groovy-all:2.0.7'
    compile 'org.mozilla:rhino:1.7R4'
    compile 'log4j:log4j:1.2.17'

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'handlebars-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'handlebars-asset-pipeline'
                    description 'Handlebars.js Transpiler Extension for the JVM Based Asset-Pipeline.'
                    url 'https://github.com/bertramdev/handlebars-asset-pipeline'
                    scm {
                        url 'https://github.com/bertramdev/handlebars-asset-pipeline'
                        connection 'scm:https://bertramdev@github.com/bertramdev/handlebars-asset-pipeline.git'
                        developerConnection 'scm:git://github.com/bertramdev/handlebars-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser: 'anonymous'
    key = project.hasProperty('bintrayKey') ? bintrayKey : null
    publications = ['maven']
    pkg {
        repo = 'asset-pipeline'
        userOrg = 'bertramlabs'
        name = 'handlebars-asset-pipeline'
        licenses = ['Apache-2.0']
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
